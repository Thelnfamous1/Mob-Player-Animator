plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'org.spongepowered.gradle.vanilla'
}
base {
    archivesName = "${mod_name}-common-${minecraft_version}"
}
minecraft {
    version(minecraft_version)
    if(file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}

repositories {
    maven { url 'https://maven.kosmx.dev/' }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation("dev.kosmx.player-anim:player-animation-lib-forge:${project.player_anim_version}")
    implementation("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}")

    // Better Mob Combat Dependencies
    implementation("maven.modrinth:better-combat:${project.better_combat_version}-forge")
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixin_extras_version}"))
    runtimeOnly("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Mod Compatibility
    implementation("maven.modrinth:entity-model-features:${project.emf_version}")
    implementation("maven.modrinth:entitytexturefeatures:${project.etf_version}-forge")
}
tasks.withType(Javadoc).configureEach {
    configure(options) {
        tags(
                'apiNote:a:"API Note:"',
                'implNote:a:"Implementation Note:"'
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}